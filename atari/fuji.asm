****************************************
* Sample kick application for Liber809
* James Wilkinson - March 12, 2012
****************************************

            use     atari.d

GRSIZE      equ     40                  ;memory per graphics mode line
GRCOUNT     equ     80                  ;total graphics mode lines
TXTSIZE     equ     40                  ;memory per text mode line
TXTCOUNT    equ     4                   ;total lines of text

CLR0        equ     $00                 ;Fuji
CLR1        equ     $0E                 ;text foreground
CLR2        equ     $00                 ;text background


****************************************
* Main entry point
            org     $2000               ;kick load address
Main

* Initialize GTIA color registers
InitClr     lda     #CLR0
            sta     COLPF0
            lda     #CLR1
            sta     COLPF1
            lda     #CLR2
            sta     COLPF2

* Convert static text, using simplified conversion to ANTIC screen characters
InitTxt     ldx     #Txt1
            ldy     #TXTSIZE*TXTCOUNT
loop@       lda     ,x
            suba    #$20
            sta     ,x+
            leay    -1,y
            bne     loop@

* Set up custom display list
InitDL      ldd     #DList
            exg     a,b
            std     DLISTL              ;point ANTIC to custom display list

* Set up and enable non-maskable interrupt
InitNMI     ldd     #NMIVect
            std     $fffc               ;point 6809 to custom interrupt vector
            lda     #$C0                
            sta     NMIEN               ;enable both display list and vertical blank interrupts

* End of main program
End         bra     End


* Custom display list:
* - 3 empty mode lines, to prevent overscan
* - 2 mode lines of ANTIC mode $2 (text)
* - 80 mode lines of ANTIC mode $D (graphics), with display list interrupt on each line
* - 2 mode lines of ANTIC mode $2 (text)
DList       fcb     AEMPTY8,AEMPTY8,AEMPTY8
            fcb     ALMS+AMODE2
            fdbs    Txt1
            fcb     AMODE2
            fcb     ALMS+ADLI+AMODED
            fdbs    FujiMem
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED,ADLI+AMODED
            fcb     ALMS+AMODE2
            fdbs    Txt2
            fcb     AMODE2
            fcb     AVB+AJMP
            fdbs    DList

* Graphics display area
            use     fujimem.asm

* Text display area
Txt1        fcc     "     Liber809 Hardware and Firmware     "
            fcc     "          (C) Boisy Pitre 2012          "
Txt2        fcc     "           Fuji Kick by Slor            "
            fcc     "      http://liber809.blogspot.com      "

ClrBase     fcb     CLR0                ;track color for first line of Fuji
ClrNext     fcb     CLR0                ;shadow storage for COLPF0


* Single vector to handle all non-maskable interrupts
NMIVect     pshs    a                   ;save register used during interrupt
            lda     NMIST
            anda    #%10000000          ;was interrupt generated by display list?
            bne     dli@                ;if so, run DLI routine
            lda     NMIST
            anda    #%01000000          ;was interrupt generated by vertical blank?
            bne     vbi@                ;if so, run VBI routine
            bra     done@
vbi@        lda     ClrBase             ;reset color for top line of Fuji
            deca                        ;adjust for scrolling effect
            sta     ClrBase             ;save for next VBI
            sta     ClrNext             ;save for next DLI
            bra     done@
dli@        lda     ClrNext             ;get color for current mode line
            adda    #2                  ;adjust for rainbow effect
            sta     WSYNC               ;wait for horizontal sync
            sta     COLPF0              ;update GTIA color register
            sta     ClrNext             ;save for next DLI
            bra     done@
done@       puls    a                   ;restore register
            rti
